sp {elaborate*query_clue_phase
(state <s> ^type state
			^name codeNames
			^role clue_giver
			-^phase load_cards
			-^phase connections
            -^phase remove
			^board <board>)
-(<s> ^clue <clue>)
-->
(<s> ^phase query_clue)
}

sp {elaborate*query_clue_phase_back
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^branch <branch>
			^clue <clue>
			-^phase load_cards
            -^phase remove
			-^phase connections)
-->
(<s> ^phase query_clue)}

sp {elaborate*query_clue_phase_back_2
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^branch <branch>
			^clue <clue>
			-^phase load_cards
            -^attempts <num>
            -^phase remove
			^max-attempts <num>)
(<clue>     ^stripped <asdf>)
-->
(<s> ^phase query_clue)}

#sp {propose*operator*query_smem_clue
#(state <s> ^type state
#			^phase query_clue
#			-^smem.command.query <cmd>)
#-->
#(<s> ^operator <o> + =)
#(<o> ^name query_clue)
#}

sp {propose*operator*query_smem_clue_2
(state <s> ^type state
			^phase query_clue
            -^attempts <num>
            ^max-attempts <num>
			-^smem.command.query <cmd>)
-->
(<s> ^operator <o> + =)
(<o> ^name query_clue)
}

sp {apply*operator*query_smem_clue
(state <s> ^type state
			^operator <o>
			^smem.command <cmd>)
(<o> ^name query_clue)
-->
(<cmd> ^query <cue>)
(<cue> ^word <word>)
}

sp {apply*operator*create_result_smem_prohibit
(state <s> ^type state
			^operator <o>
			^smem.command <cmd>
			^prohibit <lti>)
(<cmd> -^prohibit <lti>)
(<o> ^name query_clue)
-->
(<cmd> ^prohibit <lti>)
}

#propose move board to working memory
sp {propose*operator*move_results
(state <s> ^type state
			^phase query_clue
			^smem.result.success <succ>
            ^smem.result.activation <act>
			^smem.result.retrieved.word <word>)
-->
(<s> ^operator <o> + > =)
(<o> ^name move_results)
}

#propose move board to working memory
sp {propose*operator*move_results_fail
(state <s> ^type state
			^phase query_clue
			^smem.result.failure <fail>)
-->
(<s> ^operator <o> + > =)
(<o> ^name move_results_fail)
}

sp {apply*operator*move_results_fail
(state <s> ^type state
			^phase query_clue
			^operator <o>
			^smem.command <cmd>
			^max-attempts <max_num>
			^attempts <old_at>)
(<o> ^name move_results_fail)
(<cmd> ^query <elem>)
-->
(<s> ^attempts <max_num>
	^attempts <old_at> -)
(<cmd> ^query <elem> -)
}

sp {apply*operator*move_results
(state <s> ^type state
            ^attempts <att>
			^phase query_clue
			^operator <o>
			^smem.command <cmd>
			^smem.result.retrieved <lti>
            ^smem.result.activation <act>)
(<o> ^name move_results)
(<cmd> ^query <elem>)
(<lti> ^word <word>)
-->
(<s> ^clue <clue> ^attempts ( + <att> 1 ) 
	^attempts <att> -)
(<clue> ^element <lti>
		^word <word>
		^connections <con>
        ^activation <act>)
(<cmd> ^query <elem> -)
(write |guess | <word>)
}



