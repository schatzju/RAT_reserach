sp {elaborate*phase_connections
(state <s> ^type state
			^name codeNames
			^role clue_giver
			-^phase load_cards
			-^phase remove
			^board <board>
			^clue <clue>)

(<clue> -^num_assoc <num>)
-->
(<s> ^phase connections)
}

 sp {propose*operator*add_connection
(state <s> ^type state
			^name codeNames
			^phase connections
			^role clue_giver
			^clue <clue>
			^branch <branch>)
#there exists a branch whose lti has an 
#association with a clue that doesn't
#already have that association
(<clue> ^element <lti_clue1>)	
(<clue> ^word <word_p>)
(<branch> ^element <lti>
		-^neg <neg>)
(<lti> ^assoc { @ <lti_clue1> <lti_clue2>} 
		^word <word>)
(<clue> ^connections <con>)
(<con> -^word <word>)
-->
(<s> ^operator <o> + > =)
(<o> ^name add_connection)
}

sp {apply*operator*add_connection
(state <s> ^type state
			^phase connections
			^name codeNames
			^role clue_giver
			^operator <o>
			^clue <clue>
			^attempts <att>
			^branch <branch>)
#there exists a branch whose lti has an 
#association with a clue that doesn't
#already have that association
(<clue> ^element <lti_clue1>)	
(<branch> ^element <lti>
		-^neg <neg>)
(<lti> ^assoc { @ <lti_clue1> <lti_clue2>} 
		^word <word>)
(<clue> ^connections <con>)
(<con> -^word <word>)
(<o> ^name add_connection)
-->
(<con> ^word <word>)
(<clue> ^flag connects)
(<s> ^attempts ( + <att> 1 ) 
	^attempts <att> -
	^prohibit <lti_clue1>)
}

 sp {propose*operator*add_connection_neg
(state <s> ^type state
			^name codeNames
			^phase connections
			^role clue_giver
			^clue <clue>
			^branch <branch>)
#there exists a branch whose lti has an 
#association with a clue that doesn't
#already have that association
(<clue> ^element <lti_clue1>)	
(<clue> ^word <word_p>)
(<branch> ^element <lti>
			^neg <neg>)
(<branch> ^assoc { @ <lti_clue1> <lti_clue2>} 
		^word <word>)
(<clue> ^connections <con>)
(<con> -^word <word>)
-->
(<s> ^operator <o> + > =)
(<o> ^name add_connection_neg)
}

sp {apply*operator*add_connection_neg
(state <s> ^type state
			^phase connections
			^name codeNames
			^role clue_giver
			^operator <o>
			^clue <clue>
			^attempts <att>
			^branch <branch>)
#there exists a branch whose lti has an 
#association with a clue that doesn't
#already have that association
(<clue> ^element <lti_clue1>)	
(<branch> ^element <lti>
			^neg <neg>)
(<branch> ^assoc { @ <lti_clue1> <lti_clue2>} 
		^word <word>)
(<clue> ^connections <con>)
(<con> -^word <word>)
(<o> ^name add_connection_neg)
-->
(<con> ^word <word>)
(<s> ^clue <clue> -)
(<s> ^attempts ( + <att> 1 ) 
	^attempts <att> -
	^prohibit <lti_clue1>)
}

 sp {propose*operator*no_connections
(state <s> ^type state
			^name codeNames
			^phase connections
			^role clue_giver
			^clue <clue>
			^branch <branch>)
-->
(<s> ^operator <o> + =)
(<o> ^name no_connection)
}

sp {apply*operator*no_connection
(state <s> ^type state
			^phase connections
			^name codeNames
			^role clue_giver
			^operator <o>
			^clue <clue>
			^max-attempts <max_att>
			^attempts <att>
			^branch <branch>)
(<clue> ^element <lti_clue1>)	
(<o> ^name no_connection)
-->
(<clue> ^flag connects)
(<s> ^attempts ( + <att> 1 ) 
	^attempts <att> -
	^prohibit <lti_clue1>)
}

sp {preference*select_operator_neg
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o> +
			^operator <o2> +)
(<o> ^name add_connection_neg)
(<o2> ^name add_connection)
-->
(<s> ^operator <o> > <o2>)
}

sp {elaborate*count
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^clue <clue>)
(<clue> ^connections <con>
		^flag connects)
-->
(<clue> ^num_assoc (size <con>) )
}
