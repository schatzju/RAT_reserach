sp {propose*operator*result_attempts
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^clue <clue>
			^attempts <num>
			^max-attempts <num>)
#			-^phase connections) #phase connections is/was kinda janky
(<clue> ^flag connects) #The logic here is to only propose reporting results for words that have had their connections counted.
-->
(<s> ^operator <o> + > =)
(<o> ^name result
	^clue <clue>)
}

sp {propose*operator*result_goal
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^clue <clue>
			^goal-num <num_assoc>)
(<clue> ^num_assoc <num_assoc>)
-->
(<s> ^operator <o> + > =)
(<o> ^name result
	^clue <clue>)
}

sp {apply*operator*result
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o>
			^attempts <attm>
			^io <io>)
(<io> ^output-link <out>)
(<o> ^name result
	^clue <clue>)
(<clue> ^element <elem>
        ^connections <con>
		 ^word <word>
		 ^activation <act>)
-->
(<out> ^result <x>)
(<x> ^word <word>
	^num (size <con>)
	^attempts <attm>
	^activation <act>)
(write <word> | attempts: | <attm> | connections: |(size <con>) | solution: |)
(<s> ^done <x>)
}

sp {propose*operator*finished
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^done <x>)
-->
(<s> ^operator <o> + >)
(<o> ^name finished)
}

sp {apply*operator*finished_branch
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o>
			^branch <branch>)
(<o> ^name finished)
-->
(<s> ^branch <branch> -)

}

sp {apply*operator*finished_single
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o>
			^done <x>
			^board <b1>
			^clue <c1>
			^attempts <t>
			^max-attempts <mt>)
(<o> ^name finished)
-->
(<s> ^done <x> -
	^board <b1> -
	^clue <c1> -
	^attempts <t> -
	^attempts 0
	^max-attempts <mt> -)

}

sp {apply*operator*finished_pro
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o>
			^prohibit <pro>
			^smem.command <cmd>)
(<o> ^name finished)
-->
(<s> ^prohibit <pro> -
	^board <board> -
	^name RAT -
	^role solver -)
(<cmd> ^prohibit <pro> -)
}

sp {elaborate*print_solution
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^clue <clue>)
(<clue> ^word <word> ^picked <x>)
-->
(write <word> | | )

}

sp {prefrence*select_operator_finish
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o> +
			^operator <o2> +)
(<o2> ^name finished)
-->
(<s> ^operator <o2> > <o>)
}

sp {prefrence*select_operator_result_num
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o1> +
			^operator <o2> +)
(<o1> ^name result
	^clue <clue1>)
(<o2> ^name result
	^clue <clue2>)
(<clue1> ^num_assoc <num_assoc2>)
(<clue2> ^num_assoc { < <num_assoc2> <num_assoc> } )
-->
(<s> ^operator <o1> > <o2>)
}

sp {prefrence*select_operator_result_has_num
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o> +
			^operator <o2> +)
(<o2> ^name result
	^clue <clue1>)
(<o> ^name result
	^clue <clue2>)
(<clue2> -^num_assoc <num_assoc2>)
(<clue1> ^num_assoc <num_assoc> )
-->
(<s> ^operator <o> < <o2>)
}
