sp {propose*operator*get_input
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^io.input-link.board <board>
			^io.input-link.max_assoc <max>
			-^board <board1>)
-->
(<s> ^operator <o> + =)
(<o> ^name get_input)
}

sp {apply*operator*get_input
(state <s> ^type state
			^operator <o>
			^name codeNames
			^role clue_giver
			^io.input-link.board <board>
			^io.input-link.max_assoc.num <max>
			-^board <board>)
(<o> ^name get_input)
-->
(<s> ^board (deep-copy <board>))
(<s> ^max <max>)
}


sp {elaborate*not_in_wm
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^board.card <card>
			-^branch.elements.assoc.word <word>)
(<card> ^word <word>)
-->
(<card> ^needs_retrieve <var>)
}

sp {elaborate*load_cards_phase
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^board.card.needs_retrieve <var>)
-->
(<s> ^phase load_cards)
}

#if the board card isn't in working memory get clue from smem
sp {propose*operator*board_query_cards
(state <s> ^type state
			^phase load_cards
			^board.card <card>)
(<card> ^needs_retrieve <x>
		^word <word>)
-->
(<s> ^operator <o> + =)
(<o> ^name query_card
	^word <word>
	^type board)
}

sp {apply*operator*query_cards
(state <s> ^type state
			^phase load_cards
			^operator <o>
			^smem.command <cmd>)
(<o> ^name query_card
	^word <word>)
-->
(<cmd> ^query <cue>)
(<cue> ^word <word>)
}

#propose move board to working memory
sp {propose*operator*unknown_word
(state <s> ^type state
			^phase load_cards
			^smem.result.failure <fail>)
-->
(<s> ^operator <o> + >)
(<o> ^name unknown_word)
}

sp {apply*operator*unknown_word
(state <s> ^type state
			^phase load_cards
			^operator <o>
			^smem.command <cmd>
			^smem.result.failure.word <word>
			^board <board>
			^max <num>)
(<cmd> ^query <query>)
(<query> ^word <word>)
(<board> ^card <card>)
(<card> ^word <word>)
(<o> ^name unknown_word)
-->
(<cmd> ^query <query> -)
(<board> ^card <card> -)
(<s> ^max (- <num> 1) )
(<s> ^max <num> -)
}


#propose move board to working memory
sp {propose*operator*create_branch_initial
(state <s> ^type state
			^phase load_cards
			^smem.result.success <succ>
			^smem.result.retrieved.word <word>
			^board.card <card>)
(<card> ^word <word>)
-->
(<s> ^operator <o> + >)
(<o> ^name create_branch_initial
	^word <word>)
}

sp {apply*operator*create_branch_initial
(state <s> ^type state
			^phase load_cards
			^operator <o>
			^smem.command <cmd>
			^board <board>
			^smem.result.retrieved <retrieved>)
(<retrieved> ^word <word>)
(<board> ^card <card>)
(<card> ^word <word>
		-^neg <y>)
(<o> ^name create_branch_initial
	^word <word>)
(<cmd> ^query <query>)
(<query> ^word <word>)
-->
(<s> ^branch <branch>
	^prohibit <retrieved>)
(<branch> ^elements <elements>)
(<elements> ^assoc <retrieved>)
(<cmd> ^query <query> -)
}

sp {apply*operator*add_neg_card
(state <s> ^type state
			^phase load_cards
			^operator <o>
			^smem.command <cmd>
			^branch <branch>
			^smem.result.retrieved <retrieved>
			^board <board>)
(<branch> ^neg <x>
		  ^elements <elem>)
(<board> ^card <card>)
(<card> ^word <word>
		^neg <y>)
(<retrieved> ^word <word>)
(<o> ^name create_branch_initial
	^word <word>)
(<cmd> ^query <query>)
(<query> ^word <word>)
-->
(<s> ^prohibit <retrieved>)
(<elem> ^assoc <retrieved>)
(<cmd> ^query <query> -)
}

sp {prefrence*select_operator
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o> +
			-^operator <o>
			^operator <o2>
			^smem.result.success <succ>)
(<o2> ^name board_smem_to_wmem)
(<o> ^name query_card)
-->
(<s> ^operator <o> =)
}


sp {prefrence*select_operator_board
(state <s> ^type state
			^name codeNames
			^role clue_giver
			^operator <o> +
			^operator <o2> +
			^smem.result.success <succ>)
(<o2> ^name board_smem_to_wmem)
(<o> ^name board_smem_to_wmem)
-->
(<s> ^operator <o> =)
}
